grammar org.eclipse.eavp.STL with org.eclipse.xtext.common.Terminals

generate sTL "http://www.eclipse.org/eavp/STL"

import "http://www.eclipse.org/emf/2002/Ecore" as ecore


Model:
	'solid' (name = ID)?
	(triangles+=Triangle)*
	'endsolid' ID
;

Triangle:
	'facet' normal = Normal
	verticies=Verticies
	'endfacet'
;

Normal:
	'normal' vector = Vector
;

Vector:
	x = DOUBLE  y = DOUBLE  z = DOUBLE
;

Verticies:
	'outer' 'loop'
	v1 = Vertex
	v2 = Vertex
	v3 = Vertex 
	'endloop'
;

Vertex:
	'vertex'
	x = DOUBLE  y = DOUBLE  z = DOUBLE
;

terminal DOUBLE returns ecore::EDouble:
	('-')? ('0'..'9')* ('.') ('0'..'9')* (('e' | 'E') ('-' | '+')? ('0'..'9')*)?
;
